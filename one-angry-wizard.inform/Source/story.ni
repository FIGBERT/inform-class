"One Angry Wizard, or the Tentacular Adventures of our Brave Hero through a World Most Strange and Foreign" by FIGBERT

[Type Definitions]
An octopus is a kind of person. The carrying capacity of an octopus is 8. Yourself is an octopus.

A doorknob is a kind of thing. A doorknob is fixed in place.

A keyhole is a kind of container. A keyhole is open, unopenable, and fixed in place. The carrying capacity of a keyhole is 1.

A couch is a kind of container. A couch is open, unopenable, opaque, and fixed in place. A couch can be either searched or unsearched. A couch is usually unsearched.

A book is a kind of thing.

[Rooms and Items]
The Great Room is a room. The description of Great Room is "[if unvisited]Your eyes blink open, and you find yourself in the most grand room you've ever seen. The entire room is crafted from giant blocks of a sand-colored stone, stacked up to a ceiling high above where a massive mural is painted onto the rock.[paragraph break]I've trapped you in a pocket dimension (literally in my pocket, enjoy the crumbs) because I hate you. I hate you so much. You didn't buy me a present for my birthday, and now you'll pay the price[otherwise]A grand room built from giant blocks of a sand-colored stone, stacked up to a ceiling high above where a massive mural is painted onto the rock[end if].".
A closed locked door called the great metal door is east of the Great Room. "Before you to the east, taking up almost the entire wall, is a great metal door painted a deep blue and patterned with yellow and white veins that run throughout. At the base of the door are four keyholes and four doorknobs.[if the silver key is in the silver keyhole][line break]In the silver keyhole is a small silver key.[end if][if the gold key is in the gold keyhole][line break]In the gold keyhole is a small gold key.[end if][if the copper key is in the copper keyhole][line break]In the copper keyhole is a small copper key.[end if][if the brass key is in the brass keyhole][line break]In the brass keyhole is a small brass key.[end if]". The description is "Taking a closer look at the door, you see the flakes of paint chipping off with age, little smudges where the painter's brush slipped, a little mark of their humanity. Unless, actually, they're not human – given that this is a dimension dreamed up by a vengeful wizard.[if the silver key is in the silver keyhole][line break]In the silver keyhole is a small silver key.[end if][if the gold key is in the gold keyhole][line break]In the gold keyhole is a small gold key.[end if][if the copper key is in the copper keyhole][line break]In the copper keyhole is a small copper key.[end if][if the brass key is in the brass keyhole][line break]In the brass keyhole is a small brass key.[end if]".
An open unopenable door called the hallway is west of the Great Room and east of the Cluttered Room. "Behind you to the [if player is in the Great Room]west[otherwise]east[end if] you see a hallway, hewn out of the stone, leading to another room." The description of the hallway is "Looking closer, you see it's a hallway. Dipstick.".
The hidden utilities is a open unopenable container in the Great Room. The hidden utilities is scenery. The silver doorknob, the gold doorknob, the copper doorknob, and the brass doorknob are doorknobs in the hidden utilities. The silver keyhole, the gold keyhole, the copper keyhole, and the brass keyhole are keyholes in the hidden utilities. The description of the hidden utilities is "A container used to prevent doorknobs and keyholes from showing up. Props to you for finding it, but it's pretty useless.".
A thing called the mural is in the hidden utilities. The mural is fixed in place. The description of the mural is "A massive mural painted directly onto the rock of the ceiling. It depicts a raging sea of whitecaps, and a storm-tossed ship braving the ocean. In the corner, a small compass is marked on the wall. Beneath the waves, a large eight-tentacled monster lurks waiting for the ship to get in range.".
A thing called tentacles is in the hidden utilities. The description of the tentacles is "Long and purple, covered in pink suckers. Potentially useful for situations when you need more than 2 hand-things.".

The Cluttered Room is a room. The description of the Cluttered Room is "[if unvisited]You pass through the stone hallway, and step out in to the most cluttered room you've ever seen. You spin around, and bump into a tall stack of chairs like a moron.[paragraph break]You find yourself in a [otherwise]A [end if]room, half the size of the previous room, filled to the brim with chairs, folded tables, closed crates, and smaller tidbits scattered about."
An open fixed in place container called a bucket is in the Cluttered Room. "To your right you see a dented bucket[if bucket is containing things] overflowing with junk. Or maybe it's important items for progressing in the game[end if].". The description of the bucket is "A medium-sized tin bucket, paint peeling from use. It's been patched up a few times with Flex Tape in places where the damage is severe.". Inside the bucket is an umbrella, a skull, a suspicious rag, a half-eaten pie, a rare great hornd bath towl cat, a anime figurine, and a stack of sticky notes. Inside the bucket is a thing called the fish. The fish has printed name "angry fish". The fish can be either happy or angry. The fish is angry. The description of the umbrella is "A blue-and-white umbrella with a worn wooden handle. One of the spokes is bent, because the umbrella is bad and I want you to have bad things.". The description of the skull is "A skull bleached white from the sun. It looks a lot like yours will eventually.". The description of the stack of sticky notes is "A stack of yellow sticky notes with a list of items (milk, eggs, and bread if you must know). It looks suspiciously like the icon for the Sticky Notes app on macOS Big Sur because that's what it is.". The description of the suspicious rag is "A very suspicious rag. It's unusual, questionable, and maybe even dubious. It's also gray and the tag says 'Dry Erase Cleaning Cloth.' How odd.". The half-eaten pie is edible. The description of the half-eaten pie is "Half of an apple pie, clearly eaten without silverware as if it were a snack. I must have left this here accidentally." The description of the rare great hornd bath towl cat is "Kind of self explanatory.". The description of the anime figurine is "A Goku figurine. You look closer, and it punches you in the face.". The description of the fish is "[if the fish is angry]A tiny fish, undoubtably the angriest fish you've ever seen. It's not in any bag or water, which might explain its mood. Or maybe it's just hangry.[otherwise]The tiny fish smiles at you happily when you look closer at it. Though the smile is sincere, it feels odd to see a fish smile with a full mouth of human teeth[end if].".
An couch called the tattered couch is in the Cluttered Room. "The couch, lying to your left in front of the door, sags with (fake) age, and it's got tears in the tattered fabric in some places." The description of the couch is "On closer inspection, you realize that it's your couch. I stole it from the real world to put in my game. Looks like a good place to stash some stuff." Some crumbs are nowhere. The crumbs are edible. The description of the crumbs are "Some average couch crumbs from my pocket. Very important.". A thing called the silver key is nowhere. The description of the silver key is "A small silver key.".
A closed locked door called the square metal door is south of the Cluttered Room and north of the Cobble Wall. "[if the player is in the Cluttered Room]To the south, almost obscured behind a large gray couch, you see a door[otherwise]To the north you can see the door you came through earlier[end if]." The silver key unlocks the square metal door. The description of the square metal door is "A square door made of metal. It's printed from a bunch of smaller metal squares, almost like a chocolate bar. Like the door from the first room, it's painted a deep blue laced with white and yellow veins. I like that kind of door.".

The Cobble Wall is a room. The description of the Cobble Wall is "[if unvisited]You push through the door, and find yourself in a [otherwise]A [end if]tiny room. To your left a cobblestone wall stretches up high above, ending just before the ceiling. Rocks here and there seem to have fallen out of the wall over the ages it has stood in place, creating holes of varying sizes throughout the surface.". An open unopenable door called fist-sized hole is northeast of the Cobble Wall and southwest of the Cave of Shame. "[if the player is in the Cobble Wall]To the northeast, you see a hole roughly the size of a fist, punched through the rock in a feat of strength, possibly in frustration, possibly with this game[otherwise]Back to the southwest, you see the collapsed mouth of the cave[end if].". The description of the fist-sized hole is "A hole roughly the size of a fist. Definitely larger than your fist, though.". An open unopenable door called dime-sized hole is southeast of the Cobble Wall and northwest of the Teensy Cave of Fortune. "[if the player is in the Cobble Wall]To the southeast, you can see a miniscule hole, barely larger than your average United State dime, a ten-cent coin, one tenth of a United States dollar, labeled formally as 'one dime'. The denomination was first authorized by the Coinage Act of 1792. The dime is the smallest in diameter and is the thinnest of all U.S. coins currently minted for circulation, being 0.705 inches (17.91 millimeters) in diameter and 0.053 in (1.35 mm) in thickness. The hole is that size[otherwise]Behind you to the northwest, light streams in from the puny cave's entrance[end if].". The description of the dime-sized hole is "A really small hole. Seems pretty dusty inside.". A closed locked door called the center hole is east of the Cobble Wall and west of the Library. "[if the player is in the Cobble Wall]In the center of the wall to the east, you can see a hole as average as yourself. It's so normal it's disappointing[otherwise]Behind you to the west, you see a rocky door in the wall[end if].". The description of the center hole is "The hole seems almost suspiciously normal. Looking inside, it seems to end abruptly in a metal plate. The metal plate has some sort of hole or decoration, but you can't quite make out what it is.".
A thing called the cobblestone wall is in the Cobble Wall. The cobblestone wall is scenery. The description of the cobblestone wall is "A cobblestone wall stretching up almost to the ceiling. Some rocks in the walls seem to have fallen out of the wall over the ages it has stood in place, creating holes of varying sizes throughout the surface. Honestly it's just a bad wall.".

The Teensy Cave of Fortune is a room. A thing called the gold key is in the Teensy Cave of Fortune. "In the dust below your feet, a small gold key glints in the dim light.". The description of the gold key is "A small gold key.". The gold key unlocks the center hole.

The Library is a room. The description of the Library is "[if unvisited]You arrive in a long room full of thousands and thousands of bookshelves, filled to the brim with dust-covered tomes. An ancient library, with books on topics much too complex for you to understand[otherwise]A great library, full to the brim with books[end if]. The shelves continue further south."
A closed locked door called the thick metal door is east of the Library and west of the Aquarium. "To the [if the player is in the Library]east[otherwise]west[end if] you see a thick metal door[if the player is in the Library], its industrial design a strange contrast to the otherwise ancient architecture you've encountered in this world so far[end if].". The description of the thick metal door is "A thick door made of steel. It almost looks blast-proof, which it is because it's a blast-proof security door. In the bottom right corner, 'Eight Layer Protections Incorporated' is stamped into the surface.".
A closed fixed in place container called the safe is in the Library. "Extruded from the door is a safe labeled 'KEY', held closed by a 5-digit combination lock. There's a decimal point between the third and fourth digit of the number, which is odd for a combination lock.". The description of the safe is "A box attached to the door, presumably containing the key (based on the fact that the box has a sign that says 'KEY').". In the safe is a thing called the copper key. The copper key unlocks the thick metal door.

A room called the Library Shelf 1 is south of the Library. Library Shelf 1 has printed name "Library (Shelf #1)". The description of the Library Shelf 1 is "This section of the library is much the same as all the rest: shelves of books to either side, untouched books lying on their shelves. The main room is back to the north, and the shelves continue south.". A book called The Dialogues of Plato is in the Library Shelf 1. "Lying on the shelf to your left, a dustless green tome lies on its side. Gold lettering on its spine reads 'The Dialogues of Plato.' Lower down, its call number is printed: 184.2.". The description of The Dialogues of Plato is "You don't know much about this book because you're a fool, but luckily it comes with a description on the back.[paragraph break]'Socrates['] ancient words are still true, and the ideas found in Plato's Dialogues still form the foundation of a thinking person's education. This superb collection contains excellent contemporary translations selected for their clarity and accessibility to today's reader, as well as an incisive introduction by Erich Segal, which reveals Plato's life and clarifies the philosophical issues examined in each dialogue. The first four dialogues recount the trial and execution of Socrates-the extraordinary tragedy that changed Plato's life and forever altered the course of Western thought. Other dialogues create a rich tableau of intellectual life in Athens in the fourth century b.c., and examine such timeless-and timely-issues as the nature of virtue and love, knowledge and truth, society and the individual. Resounding with the humor and astounding brilliance of Socrates, the immortal iconoclast, these great works remain powerful, probing, and essential.'[paragraph break]Title – The Dialogues of Plato[line break]Call number – 184.2". A book called The Great Transformation is in the Library Shelf 1. "Standing on the right shelf, a tome with a suspicious amount of dust rests on the right shelf. Silver lettering on its spine reads 'The Great Transformation,' and below that its call number is written '200.9.'". The description of The Great Transformation is "An analysis of the peoples and religions of four distinct regions (China, India, Israel, and Greece) in the ninth century BCE, written by Karen Armstrong. Currently has 3,467 ratings at an average of 4.03 stars on Goodreads.[paragraph break]Title – The Great Transformation[line break]Call number – 200.9".

A room called the Library Shelf 2 is south of the Library Shelf 1. Library Shelf 2 has printed name "Library (Shelf #2)". The description of the Library Shelf 2 is "This section of the library is much the same as all the rest: shelves of books to either side, untouched books lying on their shelves. The library continues both north and south.". A book called Outliers is in the Library Shelf 2. "Directly in front of you, a white novel rests on the ground. Black type on its cover reads 'Outliers.' On its spine a call number is printed: 302.6.". The description of Outliers is "A pretty good book by Malcolm Gladwell, about the most famous and successful people in the world. He also draws an interesting conclusion that focuses more on their upbringing, culture, and family than personality attributes and habits – considering your utter lack of culture, I would say it's a safe bet that you're in the middle of this Gaussian curve.[paragraph break]Title – Outliers: The Story of Success[line break]Call number – 302.6". A book called Because Internet is in the Library Shelf 2. "A yellow book peeks over the edge of the right shelf. From where you stand, you can see bold black-and-blue lettering, 'Because Internet.' On its spine, a call number reads '402.231.'". The description of Because Internet is "4ll 4b0u7 7h3 1mp4c75 0f 7h3 w0rld w1d3 w3b 0n m0d3rn 3n6l15h l1n6u1571c5. fr0m c0mmun17y 5p3c1f1c j4r60n, 70 63n3r4l c0mm3n7 57ruc7ur35, 70 l4n6u4635 l1k3 l337 5p34k, 7h3 1n73rn37 h45 pr0f0undly 5h4p3d h0w w3 c0mmun1c473 - ch4n61n6 fr0m pl47f0rm 70 pl47f0rm, r00m 70 n3x7 r00m.[paragraph break]Title – Because Internet: Understanding the New Rules of Language[line break]Call number – 402.231".

A room called the Library Shelf 3 is south of the Library Shelf 2. Library Shelf 3 has printed name "Library (Shelf #3)". The description of the Library Shelf 3 is "This section of the library is much the same as all the rest: shelves of books to either side, untouched books lying on their shelves. The library continues both north and south.". A book called x is fixed in place in the Library Shelf 3. "A small glyph is carved into the wood of a shelf here that looks faintly like an 'x,' but you can't quite recognize what it is.". The description of x is "It looks a lot like an x. Maybe this is the adult section.". A thing called Eight Pies is in the Library Shelf 3. "On one of the shelves here, a dustless book rests facing you. On its cover, a detailed picture of a pie is partly obscured by the text, 'Eight Pies,' emblazoned in white. On its spine a call number is printed: 594.56.". The description of Eight Pies is "A really fantastic book about the delicious art of making eight kinds of pie, written by Roman author Oktavius Mollusca.[paragraph break]Title – Eight Pies[line break]Call number – 594.56". A book called Cult of the Dead Cow is in the Library Shelf 3. "Right next to the recipe book, a black book seems to have disturbed the dust. On its cover, yellow ASCII marks the book as 'Cult of the Dead Cow.' On its spine, a call number reads '605.8.'". The description of Cult of the Dead Cow is "An incredible classic written about the original group of elite hackers, and how they changed the landscape of computing forever. You should read it.[paragraph break]Title – Cult of the Dead Cow: How the Original Hacking Supergroup Might Just Save the World[line break]Call number – 605.8".

A room called the Library Shelf 4 is south of the Library Shelf 3. Library Shelf 4 has printed name "Library (Shelf #4)". The description of the Library Shelf 4 is "This section of the library is much the same as all the rest: shelves of books to either side, untouched books lying on their shelves. The row of shelves ends in a wall to the south, but you can head back northward.". A book called Born a Crime is in the Library Shelf 4. "In a small gap in the books, a blue book with a mural on the cover leans across the space: 'Born a Crime.' On its spine a call number is printed: 791.4502.". The description of Born a Crime is "Trevor Noah, now host of the Daily Show, writes about his childhood as a mixed child in apartheid South Africa.[paragraph break]Title – Born a Crime: Stories From A South African Childhood[line break]Call number – 791.4502.". A book called Enemy of All Mankind is in the Library Shelf 4. "A white and red book lies flat on top of the other books here. Written in black above a great red pirate flag are the words, 'Enemy of All Mankind.' On its spine, a call number reads '910.45.'". The description of Enemy of All Mankind is "I don't really know much about this book, but it's about pirates and I thought that was cool.[paragraph break]Title – Enemy of All Mankind: A True Story of Piracy, Power, and History's First Global Manhunt[line break]Call number – 910.45".

The Aquarium is a room. The description of the Aquarium is "[if unvisited]You push through the door, and find yourself in a drastically different room[otherwise]A large room[end if] with plain concrete walls and a black floor flecked with gray spots. The ceiling looks basically the same as the walls from where you're standing, but between you and me, it's a 'living roof' – a roof with a grass on top.".
An enterable container called the tank is in the Aquarium. "In the center of the room, taking up the majority of the space in the room is a giant fish tank. The bottom half meter of the tank is black glass, presumably hiding the inner workings of the tank. Above the base, the glass tank walls extend up for another two meters. There does not appear to be a lid, but a sign next to the tank says in red, 'NO MOLLUSCS IN THE TANK.'[if the player does not have the brass key] Half submerged in the sand at the bottom of the fish tank, a small metal object glints and sparkles in the light.[end if][if the fish is in the tank] The smiling fish is swimming around happily, home at last.[end if]". The tank is fixed in place. The description of the tank is "A large glash fishtank that takes up most of the space in the room, and seems to be around two and a half meters tall. [if the player does not have the brass key]Half submerged in the sand at the bottom of the fish tank, a small metal object glints and sparkles in the light. [end if]A sign next to the tanks says in red, 'NO MOLLUSCS IN THE TANK.'". A thing called the brass key is nowhere. The description of the brass key is "A small brass key.".
A closed locked door called the small blue door is north of the Aquarium and south of the Great Room. "To the [if the player is in the Aquarium]north[otherwise]south[end if], a [if the player is in the Aquarium]door that almost looks like a miniature of the grand door from the Great Room – metal blue panels with white-and-yellow veins –[otherwise]small blue door[end if] is embedded in the rock.[if the player is in the Aquarium] In the Middle Eastern style, the top of the door is a semicircle.[end if]". The brass key unlocks the small blue door. The description of the small blue door is "A [if the player is in the Aquarium]miniature of the grand door from the Great Room – metal blue panels with white-and-yellow veins –[otherwise]small blue door[end if] leads [if the player is in the Aquarium]north[otherwise]south[end if]. In the Middle Eastern style, the top of the door is a semicircle.".

[Custom Actions and Understandings]
Skipping the game is an action applying to nothing. Understand "can I skip" and "win" and "solve the puzzles" and "beat the game" as skipping the game.
Carry out skipping the game:
	say "Ok.";
	End the story.

Understand "look in" as searching. Understand "scoop [things]" and "scoop up [things]" as taking. Understand "angry fish" and "happy fish" as the fish.

Understand "average hole" and "metal" as center hole. Understand "dime hole" as dime-sized hole. Understand "fist hole" as fist-sized hole.

Understand "combination lock" and "combination" and "lock" and "code" as safe.


Entering it to is an action applying to one real number and one visible thing. Understand "enter [a real number] to [something]" and "enter [a real number] into [something]" as entering it to.
Check entering it to:
	if the second noun is not the safe:
		Say "You try to enter the code, but it's obviously not a combination lock and thus does not do anything." instead.
Report entering it to:
	Say "The safe clicks as you set the number.".

Understand "fishtank" and "aquarium" as the tank.

Understand the command "feed" as something new. Understand "feed [something] [something]" as feeding. Feeding is an action applying to two visible things.
Check feeding:
	if the noun is not the fish, say "No." instead.
Carry out feeding:	
	Now the printed name of the fish is "happy fish";
	Now the fish is happy;
	Now the second noun is nowhere;
	Say "The fish, though hesitant at first, wolfs down [the second noun] in a matter of seconds and wiggles happily. I'm sure it would be inclined to help you out if you ever needed anything fish-related.".

Understand "knob" and "door knob" as doorknob.

Winning is an action applying to one visible thing. Understand "use tentacles to unlock [something]" as winning.
Check winning:
	If the noun is not the great metal door:
		Say "What?";
		Stop the action;
	Otherwise if the silver key is not in the silver keyhole or the gold key is not in the gold keyhole or the copper key is not in the copper keyhole or the brass key is not in the brass keyhole:
		Say "I do believe those keyholes could use some work. Do you know how to use a door?";
		Stop the action;
	Otherwise:
		Continue the action.
Carry out winning:
	Say "You reach out with your tentacles, and grasp the four keys and four doorknobs. All at once, you turn them to the right. The great doors ease open with a slight creek as the world around you fades into a white nothingness, and you stand alone in a smooth plane. You are a free octopus.[paragraph break]I'll release you for now. If you bring me a present next year I might consider turning you back into a human.";
	End the story.

[Custom Interactions (i.e. instead, before, after)]
Instead of wearing the skull:
	Say "You try to put it on, but it doesn't quite fit. Odd."

After eating the half-eaten pie:
	Say "You eat the pie. It's not bad for half-eaten pie from a bucket.".

After eating the crumbs:
	Say "You eat them, and are forever lowered in my eyes you disgusting being. You should be ashamed, for what you have done is irredeemable.".

Instead of searching the tattered couch:
	If the tattered couch is unsearched:
		Say "You search throughout the couch lazily and find only crumbs. You could scoop them up I guess?";
		Now the crumbs are in the tattered couch;
		Now the tattered couch is searched;
	Otherwise:
		If the player has the crumbs and the player does not have the silver key:
			Say "You search under the couch and under the cushions, and find a small key hiding underneath.";
			Now the silver key is in the tattered couch;
		Otherwise if the player has the silver key and the player does not have the crumbs:
			Say "You search under the couch and under the cushions, and find a mountain of crumbs.";
			Now the crumbs are in the tattered couch;
		Otherwise if the player has the silver key and the player has the crumbs:
			Say "You search under the couch and under the cushions, and find... nothing.";
		Otherwise:
			Say "You search under the couch and under the cushions, and find a mountain of crumbs and a small key hiding underneath.";
			Now the silver key is in the tattered couch;
			Now the crumbs are in the tattered couch.
			

Instead of climbing the cobblestone wall in the Cobble Wall:
	Say "You try and scramble up the wall, but the surface is too rough and you fall down pathetically[paragraph break]".

Instead of going to the Cave of Shame:
	Say "You squeeze yourself through the hole with surprising ease, but due to your inability to do anything properly you cause the entrance to cave in behind you. You find yourself in a bare cave, no larger than a fist, with no way to escape your tiny prison. Maybe you should spend this valuable time before you expire thinking about what my birthday present could have been.";
	End the story.

Instead of going through the center hole:
	If the player is in the Cobble Wall:
		If the center hole is locked:
			Say "You reach into the hole and try to fit through, but the hole seems to end abruptly in a cold metal surface. Feeling around more, it feels like there is some sort of hole in the metal, but you're not sure what it is. Odd.";
		If the center hole is unlocked:
			Say "You push open the rocky door and move onwards through a short passageway.";
			Now the player is in the Library;
	Otherwise:
		Continue the action.

Instead of unlocking the center hole with the gold key:
	If the player has the gold key:
		Say "You insert the key you found into the hole in the wall, and hear a slight click. You turn the key to the left like a total dimwit, but correct yourself before the key snaps and twist the other direction. A small section of the wall eases forward with a sigh, revealing a passageway east.";
		Now the center hole is unlocked;
	Otherwise:
		Continue the action.

Instead of inserting gold key into center hole:
	Try unlocking center hole with gold key.

Instead of opening the safe:
	Say "You try to open the safe, but it's held closed by a thick combination lock.".

After entering 910.45 to the closed safe:
	Say "The lock clicks as you set the number, and begins to glow. The air around you heats up and the lid of the safe begins to rattle. Then it stops, because it's the wrong number.".

After entering 594.56 to the closed safe:
	Now the safe is open;
	Say "The safe lid pops open with a rush of air as the seal is broken. Inside, you see [a list of things in the safe].".

Instead of examining the tank:
	Say "[description of the tank][if the fish is in the tank][paragraph break]The smiling fish is swimming around happily, home at last.[end if][line break]".

Instead of climbing the tank:
	Try entering the tank.

Instead of entering the tank:
	Say "You approach the tank and scale the side with ease, slipping into the water within a matter of seconds. Suddenly, sirens go off, the room fills with a bright white light and the water quakes furiously. Armored cars and tanks smash through the walls, and the ceiling splits open for soldiers to slide in on ropes hanging from helicopters above. You're yanked out of the water, and the soldiers slap cuffs on your tentacles as they take you away.[paragraph break]That was quite foolish of you. Can't you read?";
	End the story.

Instead of inserting the fish into the tank:
	If the printed name of the fish is "happy fish":
		Say "You reach above the glass lip with a tentacle and drop the fish into the water. It gives you a little wink and dives to the bottom of the tank, disappearing into the reef. A few seconds later a small brass key flies out of the water and hits you square between the eyes, falling to the floor.";
		Now the fish is in the tank;
		Now the brass key is in the Aquarium;
	Otherwise:
		Say "You try to toss the fish into the tank, but you've never been that good at sports – the fish peaks less than a meter above your head, and slaps you in the face with its tail on the way back down. Maybe it would be more inclined to help you if it wasn't starving.";
		Stop the action.

Instead of unlocking the great metal door with something:
	If the second noun is the silver key:
		Say "You put the first key in the silver keyhole. You turn it to the right, but it doesn't budge. You don't appear to be able to turn this key by itself.";
		Try silently inserting the silver key into the silver keyhole;
		Stop the action;
	If the second noun is the gold key:
		Say "You put the gold key in the gold keyhole. You turn it to the right, but it doesn't budge. You don't appear to be able to turn this key by itself.";
		Try silently inserting the gold key into the gold keyhole;
		Stop the action;
	If the second noun is the copper key:
		Say "You put the copper key in the copper keyhole. You turn it to the right, but it doesn't budge. You don't appear to be able to turn this key by itself.";
		Try silently inserting the copper key into the copper keyhole;
		Stop the action;
	If the second noun is the brass key:
		Say "You put the brass key in the brass keyhole. You turn it to the right, but it doesn't budge. You don't appear to be able to turn this key by itself.";
		Try silently inserting the brass key into the brass keyhole;
		Stop the action;
	If the second noun is the tentacles and the silver key is in the silver keyhole and the gold key is in the gold keyhole and the copper key is in the copper keyhole and the brass key is in the brass keyhole:
		Try silently winning the great metal door;
	Otherwise:
		Say "What?";
		Stop the action.

Instead of turning a doorknob, say "You turn [the noun], and nothing happens. You don't appear to be able to turn this doorknob by itself.".

Instead of examining the hidden utilities:
	Say "[description of the hidden utilities][line break]".

Every turn:
	Now the tentacles are in the hidden utilities.

[Release Modifications]
The story headline is "A puzzle game about an angry wizard".
The story genre is "Mystery".
The story description is "You're trapped in an alternate dimension inside the pocket of a very angry wizard. Escape.".
The story creation year is 0.
The release number is 1.
Release along with an interpreter.